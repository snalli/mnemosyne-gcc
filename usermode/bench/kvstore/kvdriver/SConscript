Import('kvstoreEnv')
Import('mcoreLibrary')
Import('mtmLibrary')
Import('mallocLibrary')
Import('hashtableLibrary')

myEnv = kvstoreEnv.Clone() 
myEnv.Append(CCFLAGS = ' -D_GNU_SOURCE ')
myEnv.Append(CPPPATH = ['#library/common'])
myEnv.Append(CPPPATH = ['#bench/kvstore/ht'])
myEnv.Append(CPPPATH = ['#bench/kvstore/kvdriver/common'])

myEnv.Append(CPPPATH = myEnv['EXPLICIT_DEPENDENCY']['BDB_INCLUDE'] )
myEnv.Append(LINKFLAGS = '-Wl,--whole-archive %s -Wl,--no-whole-archive' % myEnv['EXPLICIT_DEPENDENCY']['BDB_LIB'])

myEnv.Append(LIBS = [mcoreLibrary])
myEnv.Append(LIBS = [mtmLibrary])
myEnv.Append(LIBS = [mallocLibrary])

mainObj = myEnv.Object('common/main.c')


# Hashtable & BDB kvdriver

htEnv = myEnv.Clone() 
sources_ht = Split("""
		   ht/bdb_mix.c
		   ht/mtm_mix.c
		   ht/ubench.c
                   """)


htEnv.Append(CPPPATH = ['#bench/kvstore/kvdriver/ht'])
htEnv.Append(LIBS = [hashtableLibrary])
htEnv.Program('kvdriver-ht', sources_ht + mainObj)



# TokyoCabinet kvdriver
#
# We build a separate kvdriver for the two flavors of TokyoCabinet as 
# we cannot link kvdriver to both TokyoCabinet libraries.



sources_tc_base = Split("""
		        tc/ubench-base.c
			tc/tc-base_mix.c
                        """)
			

tcbaseEnv = myEnv.Clone() 
tcbaseEnv.Append(CPPPATH = myEnv['EXPLICIT_DEPENDENCY']['TC_BASE_INCLUDE'] )
tcbaseEnv.Append(LINKFLAGS = '-Wl,--whole-archive %s -Wl,--no-whole-archive' % myEnv['EXPLICIT_DEPENDENCY']['TC_BASE_LIB'])
tcbaseEnv.Append(LINKFLAGS = '-lbz2 -lz')
tcbaseEnv.Program('kvdriver-tc-base', sources_tc_base + mainObj)


sources_tc_mtm = Split("""
		        tc/ubench-mtm.c
			tc/tc-mtm_mix.c
                        """)

tcmtmEnv = myEnv.Clone() 
tcmtmEnv.Append(CPPPATH = myEnv['EXPLICIT_DEPENDENCY']['TC_MTM_INCLUDE'] )
tcmtmEnv.Append(LINKFLAGS = '-Wl,--whole-archive %s -Wl,--no-whole-archive' % myEnv['EXPLICIT_DEPENDENCY']['TC_MTM_LIB'])
tcmtmEnv.Append(LINKFLAGS = '-lbz2 -lz')
tcmtmEnv.Program('kvdriver-tc-mtm', sources_tc_mtm + mainObj)
